//+------------------------------------------------------------------+
//|                                                TBPriceAction.mq5 |
//|                                 Copyright 2000-2023, Alex zhang. |
//|                                             im.zhangjf@gmail.com |
//+------------------------------------------------------------------+
#property copyright "Copyright 2000-2023, Alex zhang."
#property link      "im.zhangjf@gmail.com"
#property version   "1.00"

#include <Trade\Trade.mqh>

//持仓结构
struct TradeInfo
  {
   int               direction; //交易方向,1:买入,2:卖出
   datetime          outTime; //出场时间
   double            outPrice; //出场价格
   double            entryPrice; //入场价格
   double            stopPrice; //原始止损
   double            moveStopPrice; //移动止损
   double            profitRatio; //盈亏比
   double            maxProfitRatio; //持仓达到的最大盈亏比
   bool              isWin; //是否盈利
   datetime          entryTime; //入场时间
   double            lot; //开仓数量
  };
//入参
input double Commission=7; //1标准手佣金(买/卖)
input bool BreakEvenIncludeCommision=true; //推BE时是否考虑包含佣金,true:包含,false:不包含
input bool IsFixedLot=false; //是否使用固定开仓手数,true:是,将使用固定开仓手数参数开仓,false:否,将使用最大止损金额参数计算手数(保证每次开仓止损一样大)
input double FixedOrderLot=1; //固定开仓手数
input double StopLimit= 100.00;    // 最大止损金额
input int StopPoint=0;    // 止损加点数,在止损基础上增加的保护点数,只对初始止损有效
input int StopPointEffectiveRange=0; //计算止损时判断止损点的有效范围:止损点附近最高到最低的范围太小的话,不算作最低点
input bool FollowMainFrameTrend=true; //突破趋势方向是否需要跟主周期一致,true:需要,false:不需要
input ENUM_TIMEFRAMES MainTimeFrames=PERIOD_D1; //主均线周期,默认:天
input double ProfitProtectionRate=0.5; //推BE盈亏比,价格达到多少倍盈亏比时,将止损移动到入场位,0为不推BE
input double StopWinProfitRatio=1; //主动止盈的最大盈亏比,达到该盈亏比后进行止盈,为0表示不主动止盈
input int NoTradeTimeBegin=-1; //不进行交易的时间起,包含(单位:hour,交易服务器时间),-1表示全部时间段都进行交易
input int NoTradeTimeEnd=-1; //不进行交易的时间止,不包含(单位:hour交易服务器时间),-1表示全部时间段都进行交易
input double EmaSubtracted=0; //过滤条件:20EMA跟220EMA相差的有效值,如果差值太小,说明可能是缠绕状态,过滤避免做单
input double BodyFusesRatio=2; //过滤条件:突破最后一根蜡烛的烛身跟引线的比例,避免引线过长的情况

//本周期均线
int    ExtHandleMa20=0;
int    ExtHandleMa35=0;
int    ExtHandleMa40=0;
int    ExtHandleMa45=0;
int    ExtHandleMa50=0;
int    ExtHandleMa60=0;
int    ExtHandleMa220=0;
//本周期布林带
int    ExtHandleBands=0;
//大周期均线
int    ExtHandleMainMa35=0;
int    ExtHandleMainMa40=0;
int    ExtHandleMainMa45=0;
int    ExtHandleMainMa50=0;
int    ExtHandleMainMa60=0;

bool   IsPostioned=false;  //是否已开仓
double ContractSize=0;     //标准合约大小
double SymbolPoint=0;      //交易品种点值
uint   Serial=0;           //交易序号
int    AccountId=0;        //交易账号
bool   IsRealAccount=false;//是否真实账户,true:是,false:否
string GlobalSerialName="";//保存序号的全局变量名称
bool   PrintRangePoint=false;   //输出RangePoint信息
bool   PrintBreakEven=false;    //输出breakeven信息
double TradeLowestPrice=0; //三根连续K线的最低价
double TradeHighestPrice=0; //三根连续K线的最高价

bool   ExtHedging=false;
CTrade ExtTrade;
TradeInfo tradeInfo;

#define MA_MAGIC 39518605

//计算点值
double SymbolPointValue(void)
  {
   double openPrice=NormalizeDouble(tradeInfo.entryPrice, Digits());
   double commissionPoint=0;
//USD在后面的直盘:EURUSD
   if(StringFind(_Symbol,"USD") > 0)
     {
      commissionPoint=NormalizeDouble(Commission*tradeInfo.lot/(SymbolPoint*ContractSize)*SymbolPoint,Digits());
     }
//USD在前面的直盘:USDJPY
   else
      if(StringFind(_Symbol,"USD") == 0)
        {
         commissionPoint=NormalizeDouble(Commission*tradeInfo.lot/(SymbolPoint*ContractSize/openPrice)*SymbolPoint,Digits());
        }
      //交叉盘:NZDJPY
      else
         if(StringFind(_Symbol,"USD") <= 0)
           {
            string symbol=StringSubstr(_Symbol,0,3)+"USD";
            MqlTick tick;
            SymbolInfoTick(symbol, tick);
            double tickask=NormalizeDouble(tick.ask,SymbolInfoInteger(symbol,SYMBOL_DIGITS));
            commissionPoint=NormalizeDouble(Commission*tradeInfo.lot/(SymbolPoint*ContractSize*tickask/openPrice)*SymbolPoint,Digits());
           }
//--- return commissionPoint volume
   return(commissionPoint);
  }
//+------------------------------------------------------------------+
//| Calculate optimal lot size                                       |
//+------------------------------------------------------------------+
double TradeSizeOptimized(void)
  {
   int leverrage=AccountInfoInteger(ACCOUNT_LEVERAGE);
   double openPrice=NormalizeDouble(tradeInfo.entryPrice, Digits());
   double stopPrice=NormalizeDouble(tradeInfo.stopPrice, Digits());
//--- 计算仓位
   double lot=0;
//USD在后面的直盘:EURUSD
   if(StringFind(_Symbol,"USD") > 0)
     {
      lot=NormalizeDouble(StopLimit/(MathAbs(openPrice-stopPrice)*ContractSize),2);
     }
//USD在前面的直盘:USDJPY
   else
      if(StringFind(_Symbol,"USD") == 0)
        {
         lot=NormalizeDouble(StopLimit/(MathAbs(openPrice-stopPrice)*ContractSize/openPrice),2);
        }
      //交叉盘:NZDJPY
      else
         if(StringFind(_Symbol,"USD") <= 0)
           {
            string symbol=StringSubstr(_Symbol,0,3)+"USD";
            MqlTick tick;
            SymbolInfoTick(symbol, tick);
            double tickask=NormalizeDouble(tick.ask,SymbolInfoInteger(symbol,SYMBOL_DIGITS));
            lot=NormalizeDouble(StopLimit/(MathAbs(openPrice-stopPrice)*ContractSize*tickask/openPrice),2);
           }
//边界检查
   double minvol=SymbolInfoDouble(_Symbol,SYMBOL_VOLUME_MIN);
   if(lot<minvol)
      lot=minvol;

   double maxvol=SymbolInfoDouble(_Symbol,SYMBOL_VOLUME_MAX);
   if(lot>maxvol)
      lot=maxvol;
//--- return trading volume
   return(lot);
  }
//格式化TradeInfo价格位数
void FormatTradeInfo()
  {
   if(tradeInfo.entryPrice!=NULL)
     {
      tradeInfo.entryPrice=NormalizeDouble(tradeInfo.entryPrice,Digits());
     }
   if(tradeInfo.outPrice!=NULL)
     {
      tradeInfo.outPrice=NormalizeDouble(tradeInfo.outPrice,Digits());
     }
   if(tradeInfo.stopPrice!=NULL)
     {
      tradeInfo.stopPrice=NormalizeDouble(tradeInfo.stopPrice,Digits());
     }
   if(tradeInfo.moveStopPrice!=NULL)
     {
      tradeInfo.moveStopPrice=NormalizeDouble(tradeInfo.moveStopPrice,Digits());
     }
   if(tradeInfo.profitRatio!=NULL)
     {
      tradeInfo.profitRatio=NormalizeDouble(tradeInfo.profitRatio,2);
     }
  }
//+------------------------------------------------------------------+
//| Check for open position conditions                               |
//+------------------------------------------------------------------+
void CheckForOpen(void)
  {
   if(IsPostioned==true)
     {
      return;
     }
//获取当前K线数据
   MqlRates rt[2];
//--- go trading only for first ticks of new bar
   if(CopyRates(_Symbol,_Period,0,2,rt)!=2)
     {
      Print("CopyRates of ",_Symbol," failed, no history");
      return;
     }
   if(rt[1].tick_volume>1)
      return;
   MqlRates current=rt[0]; //rt[0]其实是前一根K线
//获取当前报价
   MqlTick tick;
   if(!SymbolInfoTick(_Symbol, tick))
     {
      Print("SymbolInfoTick of ",_Symbol," failed, no tick");
      return;
     }
//测试时间用断点
   if(current.time==StringToTime("2022.01.28 00:00:00"))
     {
      printf("进入断点");
     }
//获取时间
   MqlDateTime mdt;
   TimeToStruct(current.time, mdt);
//如果在非交易时间范围内,不进行交易
   if(NoTradeTimeBegin!=-1&&NoTradeTimeEnd!=-1)
     {
      if(NoTradeTimeBegin<NoTradeTimeEnd)
        {
         if(mdt.hour>=NoTradeTimeBegin&&mdt.hour<NoTradeTimeEnd)
           {
            return;
           }
        }
      else
         if(NoTradeTimeBegin>NoTradeTimeEnd)
           {
            if(mdt.hour>=NoTradeTimeBegin||mdt.hour<NoTradeTimeEnd)
              {
               return;
              }
           }
     }
   else
      if(NoTradeTimeBegin!=-1)
        {
         if(mdt.hour>=NoTradeTimeBegin)
           {
            return;
           }
        }
      else
         if(NoTradeTimeEnd!=-1)
           {
            if(mdt.hour<NoTradeTimeEnd)
              {
               return;
              }
           }
//--- 获取均线组数据判断趋势
   double   ma35[2];
   if(CopyBuffer(ExtHandleMa35,0,0,2,ma35)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   ma40[2];
   if(CopyBuffer(ExtHandleMa40,0,0,2,ma40)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   ma45[2];
   if(CopyBuffer(ExtHandleMa45,0,0,2,ma45)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   ma50[2];
   if(CopyBuffer(ExtHandleMa50,0,0,2,ma50)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   ma60[2];
   if(CopyBuffer(ExtHandleMa60,0,0,2,ma60)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   bandsMid[2];
   if(CopyBuffer(ExtHandleBands,0,0,2,bandsMid)!=2)
     {
      Print("CopyBuffer from iBands upper failed, no data");
      return;
     }
   double   bandsUpper[2];
   if(CopyBuffer(ExtHandleBands,1,0,2,bandsUpper)!=2)
     {
      Print("CopyBuffer from iBands upper failed, no data");
      return;
     }
   double   bandslower[2];
   if(CopyBuffer(ExtHandleBands,2,0,2,bandslower)!=2)
     {
      Print("CopyBuffer from iBands lower failed, no data");
      return;
     }
   double   mainMa35[2];
   if(CopyBuffer(ExtHandleMainMa35,0,0,2,mainMa35)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   mainMa40[2];
   if(CopyBuffer(ExtHandleMainMa40,0,0,2,mainMa40)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   mainMa45[2];
   if(CopyBuffer(ExtHandleMainMa45,0,0,2,mainMa45)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   mainMa50[2];
   if(CopyBuffer(ExtHandleMainMa50,0,0,2,mainMa50)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   mainMa60[2];
   if(CopyBuffer(ExtHandleMainMa60,0,0,2,mainMa60)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
//--- 检查是否开仓
   bool signal=false;
   ENUM_ORDER_TYPE direction=WRONG_VALUE; //开仓方向
//步骤一.判断做单方向
//检查均线差值
   double   ma20[2];
   if(CopyBuffer(ExtHandleMa20,0,0,2,ma20)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   ma220[2];
   if(CopyBuffer(ExtHandleMa220,0,0,2,ma220)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
//判断均线差值是否满足开仓条件
   if(MathAbs(ma220[0]-ma20[0])/SymbolPoint<EmaSubtracted)
     {
      return;
     }
//做单方向
   direction=ma20[0]>ma220[0]?ORDER_TYPE_BUY:ORDER_TYPE_SELL;
   if(direction==WRONG_VALUE)
      return;
//步骤二.判断前三根K线是否都为阳线或者都为阴线
//获取前三根K线,判断是否都为阳线或阴线,需要跟做单方向一致,并且最后一跟K线需要超过20EMA,引线需要小于实体
   MqlRates trendRt[4];
   if(CopyRates(_Symbol,_Period,0,4,trendRt)!=4)
     {
      Print("CopyRates of ",_Symbol," failed, no history");
      return;
     }
   MqlRates first=trendRt[0],second=trendRt[1],third=trendRt[2];
   if(direction==ORDER_TYPE_BUY)
     {
      if(first.close<=first.open)
         return;
      if(second.close<=second.open)
         return;
      if(third.close<=third.open)
         return;
      if(third.close<=ma20[0])
         return;
      if((third.close-third.open)/(third.high-third.close)<BodyFusesRatio||(first.close-first.open)/(first.high-first.close)<BodyFusesRatio||(second.close-second.open)/(second.high-second.close)<BodyFusesRatio)
         return;
      TradeLowestPrice=MathMin(MathMin(first.low,second.low),third.low);
      TradeHighestPrice=MathMax(MathMax(first.high,second.high),third.high);
     }
   else
      if(direction==ORDER_TYPE_SELL)
        {
         if(first.close>=first.open)
            return;
         if(second.close>=second.open)
            return;
         if(third.close>=third.open)
            return;
         if(third.close>=ma20[0])
            return;
         if((third.open-third.close)/(third.close-third.low)<BodyFusesRatio||(first.open-first.close)/(first.close-first.low)<BodyFusesRatio||(second.open-second.close)/(second.close-second.low)<BodyFusesRatio)
            return;
         TradeLowestPrice=MathMin(MathMin(first.low,second.low),third.low);
         TradeHighestPrice=MathMax(MathMax(first.high,second.high),third.high);
        }
//步骤三.判断是否需要跟主周期趋势方向一致
   double ma[]= {ma35[0],ma40[0],ma45[0],ma50[0],ma60[0]};
   int maSequence=CalcMaSequence(ma);
//大周期均线排列
   double mainMa[]= {mainMa35[0],mainMa40[0],mainMa45[0],mainMa50[0],mainMa60[0]};
   int mainMaSequence=CalcMaSequence(mainMa);
//是否跟主周期一致
   if(FollowMainFrameTrend==false||(direction==ORDER_TYPE_BUY&&mainMaSequence==1)||(direction==ORDER_TYPE_SELL&&mainMaSequence==-1))
     {
      signal=true;
     }
   if(!signal)
     {
      return;
     }
//填充开仓信息
   tradeInfo.direction=direction==ORDER_TYPE_BUY?1:2;
   tradeInfo.entryPrice=direction==ORDER_TYPE_BUY?tick.ask:tick.bid;
   tradeInfo.stopPrice=direction==ORDER_TYPE_BUY?TradeLowestPrice-(StopPoint*SymbolPoint):TradeHighestPrice+(StopPoint*SymbolPoint);
   tradeInfo.moveStopPrice=tradeInfo.stopPrice;
   tradeInfo.lot=IsFixedLot?FixedOrderLot:TradeSizeOptimized();
   tradeInfo.maxProfitRatio=0.0;
   tradeInfo.entryTime=TimeLocal();
//--- 开仓
   if(direction!=WRONG_VALUE && signal)
     {
      if(TerminalInfoInteger(TERMINAL_TRADE_ALLOWED) && Bars(_Symbol,_Period)>100)
        {
         FormatTradeInfo();
         Serial=Serial+1;
         //循环开仓,确保开成功
         int i=1;
         do
           {
            ExtTrade.PositionOpen(_Symbol,direction,tradeInfo.lot,SymbolInfoDouble(_Symbol,direction==ORDER_TYPE_SELL ? SYMBOL_BID:SYMBOL_ASK),tradeInfo.stopPrice,0,IntegerToString(Serial));
            Sleep(500);
            uint retCode=ExtTrade.ResultRetcode();
            printf(StringFormat("品种:%s_%s,编号:%d,第%d次开仓,开仓结果代码:%d",_Symbol,StringSubstr(EnumToString(_Period),7),Serial,i++,retCode));
            if(retCode==TRADE_RETCODE_DONE)
              {
               break;
              }
            if(i>20)
              {
               //开仓失败,发送邮件
               string text=StringFormat("开仓失败,%s账号:%d,品种:%s_%s,编号:%d,错误代码:%d,错误内容:%s",IsRealAccount?"实盘":"模拟",AccountId,_Symbol,StringSubstr(EnumToString(_Period),7),Serial,retCode,transReturnCode(retCode));
               printf(text);
               string title=StringFormat("开仓失败,%s账号:%d,品种:%s_%s,编号:%d",IsRealAccount?"实盘":"模拟",AccountId,_Symbol,StringSubstr(EnumToString(_Period),7),Serial);
               SendMail(title, text);
               SendNotification(text);
               return;
              }
           }
         while(i<=20);

         string text=StringFormat("开仓成功,%s账号:%d,品种:%s_%s,编号:%d,开仓数量:%.2f,方向:%s,开仓价格:%f,止损价格:%f,开仓时间:%s",IsRealAccount?"实盘":"模拟",AccountId,_Symbol,StringSubstr(EnumToString(_Period),7),Serial,tradeInfo.lot,direction==ORDER_TYPE_SELL ? "卖开":"买开",tradeInfo.entryPrice,tradeInfo.stopPrice,TimeToString(tradeInfo.entryTime, TIME_DATE|TIME_SECONDS));
         printf(text);
         //打印推BE信息
         PrintBreakEven=true;
         //发送开仓邮件
         string title=StringFormat("开仓成功,%s账号:%d,品种:%s_%s,编号:%d",IsRealAccount?"实盘":"模拟",AccountId,_Symbol,StringSubstr(EnumToString(_Period),7),Serial);
         SendMail(title, text);
         //发送开仓通知
         SendNotification(text);
         //更新全局变量
         GlobalVariableSet(GlobalSerialName, Serial);
         GlobalVariablesFlush();
        }
     }
//---
  }
//判断均线多头还是空头
int CalcMaSequence(double &ma[])
  {
//判断是否有值
   for(int i=0; i<ArraySize(ma)-1; i++)
     {
      if(ma[i]==0)
        {
         return 0;
        }
     }
//判断是否均线多头
   bool isHigher=true;
   for(int i=0; i<ArraySize(ma)-2; i++)
     {
      if(ma[i]<=ma[i+1])
        {
         isHigher=false;
         break;
        }
     }
   if(isHigher)
     {
      return 1;
     }
//判断是否均线空头
   bool isLower=true;
   for(int i=0; i<ArraySize(ma)-2; i++)
     {
      if(ma[i]>=ma[i+1])
        {
         isLower=false;
         break;
        }
     }
   if(isLower)
     {
      return -1;
     }
   return 0;
  }
//计算K线最低点和最高点
double CalaStopPrice(ENUM_ORDER_TYPE direction)
  {
//获取100根K线
   MqlRates rates[];
   ArraySetAsSeries(rates,true);
   if(CopyRates(_Symbol,_Period,0,100,rates)!=100)
     {
      Print("CopyRates of ",_Symbol," failed, no history");
      return 0;
     }
   double highPrice =rates[0].high;
   double lowPrice = rates[0].low;
   int state = 1;//当前状态,1:寻找止损点,2,找到止损点后判断是否符合
   for(int i=1;i<100;i++)
     {
      if(direction==ORDER_TYPE_BUY)
        {
         if(state==1)
           {
            if(rates[i].low<=lowPrice)
              {
               lowPrice=rates[i].low;
              }
            else
              {
               highPrice=rates[i].high;
               state=2;
              }
           }
         else
            if(state==2)
              {
               if(rates[i].high>=highPrice)
                 {
                  highPrice=rates[i].high;
                 }
               else
                 {
                  if((highPrice-lowPrice) < StopPointEffectiveRange)
                    {
                     //止损点的最高点跟最低点相差太小,认为并不是一个有效的最高最低点,重新选择
                     state=1;
                     lowPrice=rates[i].low;
                    }
                  else
                    {
                     //找到符合条件的最低点,计算止损价位
                     return lowPrice-(StopPoint*SymbolPoint);
                    }
                 }
              }
        }
      else
         if(direction==ORDER_TYPE_SELL)
           {
            if(state==1)
              {
               if(rates[i].high>=highPrice)
                 {
                  highPrice=rates[i].high;
                 }
               else
                 {
                  lowPrice=rates[i].low;
                  state=2;
                 }
              }
            else
               if(state==2)
                 {
                  if(rates[i].low<=lowPrice)
                    {
                     lowPrice=rates[i].low;
                    }
                  else
                    {
                     if((highPrice-lowPrice) < StopPointEffectiveRange)
                       {
                        //止损点的最低点跟最高点相差太小,认为并不是一个有效的最高最低点,重新选择
                        state=1;
                        highPrice=rates[i].high;
                       }
                     else
                       {
                        //找到符合条件的最低点,计算止损价位
                        return highPrice+(StopPoint*SymbolPoint);
                       }
                    }
                 }
           }
     }
   return 0;
  }
//+------------------------------------------------------------------+
//| Check for close position conditions                              |
//+------------------------------------------------------------------+
void CheckForClose(void)
  {
   if(IsPostioned==false)
     {
      return;
     }
//--- 获取均线和布林带数据
   double   bandsMid[1];
   if(CopyBuffer(ExtHandleBands,0,0,1,bandsMid)!=1)
     {
      Print("CopyBuffer from iBands upper failed, no data");
      return;
     }
   double   bandsUpper[1];
   if(CopyBuffer(ExtHandleBands,1,0,1,bandsUpper)!=1)
     {
      Print("CopyBuffer from iBands upper failed, no data");
      return;
     }
   double   bandslower[1];
   if(CopyBuffer(ExtHandleBands,2,0,1,bandslower)!=1)
     {
      Print("CopyBuffer from iBands lower failed, no data");
      return;
     }
   double   ma35[2];
   if(CopyBuffer(ExtHandleMa35,0,0,2,ma35)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   ma40[2];
   if(CopyBuffer(ExtHandleMa40,0,0,2,ma40)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   ma45[2];
   if(CopyBuffer(ExtHandleMa45,0,0,2,ma45)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   ma50[2];
   if(CopyBuffer(ExtHandleMa50,0,0,2,ma50)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
   double   ma60[2];
   if(CopyBuffer(ExtHandleMa60,0,0,2,ma60)!=2)
     {
      Print("CopyBuffer from iMA failed, no data");
      return;
     }
//获取当前报价
   MqlTick tick;
   if(!SymbolInfoTick(_Symbol, tick))
     {
      Print("SymbolInfoTick of ",_Symbol," failed, no tick");
      return;
     }
//测试时间用断点
   if(tick.time==StringToTime("2022.01.28 10:00:00"))
     {
      printf("进入断点");
     }
//--- 判断是否需要平仓
   bool signal=false;
//判断出场条件
   if(tradeInfo.direction==1)
     {
      double bid=SymbolInfoDouble(_Symbol,SYMBOL_BID);
      //判断止损
      if(bid<=tradeInfo.moveStopPrice)
        {
         tradeInfo.outTime=TimeLocal();
         tradeInfo.outPrice=tradeInfo.moveStopPrice;
         tradeInfo.profitRatio=tradeInfo.entryPrice==tradeInfo.moveStopPrice?0:-1;
         tradeInfo.isWin=tradeInfo.entryPrice==tradeInfo.moveStopPrice?true:false;
         signal=true;
        }
      //判断是否要推止损,当前盈亏比比持仓最大的盈亏比要大,说明可能需要推止损
      double profitRatio = (bid-tradeInfo.entryPrice)/(tradeInfo.entryPrice-tradeInfo.stopPrice);
      if(profitRatio>tradeInfo.maxProfitRatio)
        {
         tradeInfo.maxProfitRatio=profitRatio;
         //盈亏比大于了设定的盈亏比,止损到入场点
         if(ProfitProtectionRate!=0&&profitRatio>=ProfitProtectionRate)
           {
            tradeInfo.moveStopPrice=tradeInfo.entryPrice;
            if(BreakEvenIncludeCommision)
              {
               tradeInfo.moveStopPrice=tradeInfo.moveStopPrice+SymbolPointValue();
              }
            if(PrintBreakEven)
              {
               printf("BreankEven,盈亏比:%f,达到设定BE盈亏比:%f,移动止损至入场价位:%f", profitRatio, ProfitProtectionRate,tradeInfo.moveStopPrice);
               PrintBreakEven=false;
              }
           }
        }
      if(!signal)
        {
         //止盈
         //判断是否达到最大盈亏比止盈
         double profitRatio = (bid-tradeInfo.entryPrice)/(tradeInfo.entryPrice-tradeInfo.stopPrice);
         if(StopWinProfitRatio != 0 && profitRatio >= StopWinProfitRatio)
           {
            tradeInfo.outTime=TimeLocal();
            tradeInfo.outPrice=bid;
            tradeInfo.profitRatio=profitRatio;
            tradeInfo.isWin=true;
            signal=true;
            printf("Take Profit,盈亏比:%f,达到设定止盈盈亏比:%f,止盈价位:%f,盈利点数:%f", profitRatio, StopWinProfitRatio,tradeInfo.outPrice,MathAbs((tradeInfo.outPrice-tradeInfo.entryPrice)/SymbolPoint));
           }
        }
     }
   else
      if(tradeInfo.direction==2)
        {
         double ask=SymbolInfoDouble(_Symbol,SYMBOL_ASK);
         //判断止损
         if(ask>=tradeInfo.moveStopPrice)
           {
            tradeInfo.outTime=TimeLocal();
            tradeInfo.outPrice=tradeInfo.moveStopPrice;
            tradeInfo.profitRatio=tradeInfo.entryPrice==tradeInfo.moveStopPrice?0:-1;
            tradeInfo.isWin=tradeInfo.entryPrice==tradeInfo.moveStopPrice?true:false;
            signal=true;
           }
         //判断是否要推止损,当前盈亏比比持仓最大的盈亏比要大,说明可能需要推止损
         double profitRatio = (tradeInfo.entryPrice-ask)/(tradeInfo.stopPrice-tradeInfo.entryPrice);
         if(profitRatio>tradeInfo.maxProfitRatio)
           {
            tradeInfo.maxProfitRatio=profitRatio;
            //盈亏比大于了设定的盈亏比,止损到入场点
            if(ProfitProtectionRate!=0&&profitRatio>=ProfitProtectionRate)
              {
               tradeInfo.moveStopPrice=tradeInfo.entryPrice;
               if(BreakEvenIncludeCommision)
                 {
                  tradeInfo.moveStopPrice=tradeInfo.moveStopPrice-SymbolPointValue();
                 }
               if(PrintBreakEven)
                 {
                  printf("BreankEven,盈亏比:%f,达到设定BE盈亏比:%f,移动止损至入场价位:%f", profitRatio, ProfitProtectionRate,tradeInfo.moveStopPrice);
                  PrintBreakEven=false;
                 }
              }
           }
         if(!signal)
           {
            //止盈
            //判断是否达到最大盈亏比止盈
            double profitRatio = (tradeInfo.entryPrice-ask)/(tradeInfo.stopPrice-tradeInfo.entryPrice);
            if(StopWinProfitRatio != 0 && profitRatio >= StopWinProfitRatio)
              {
               tradeInfo.outTime=TimeLocal();
               tradeInfo.outPrice=ask;
               tradeInfo.profitRatio=profitRatio;
               tradeInfo.isWin=true;
               signal=true;
               printf("Take Profit,盈亏比:%f,达到设定止盈盈亏比:%f,止盈价位:%f,盈利点数:%f", profitRatio, StopWinProfitRatio,tradeInfo.outPrice,MathAbs((tradeInfo.outPrice-tradeInfo.entryPrice)/SymbolPoint));
              }
           }
        }
//--- 平仓
   if(signal)
     {
      if(TerminalInfoInteger(TERMINAL_TRADE_ALLOWED) && Bars(_Symbol,_Period)>100)
        {
         FormatTradeInfo();
         //循环平仓,确保平掉
         int i=1;
         do
           {
            ExtTrade.PositionClose(_Symbol);
            Sleep(500);
            uint retCode=ExtTrade.ResultRetcode();
            printf(StringFormat("品种:%s_%s,编号:%d,第%d次平仓,平仓结果代码:%d",_Symbol,StringSubstr(EnumToString(_Period),7),Serial,i++,retCode));
            if(retCode==TRADE_RETCODE_DONE||retCode==TRADE_RETCODE_POSITION_CLOSED)
              {
               break;
              }
            if(i>20)
              {
               //平仓失败,发送邮件
               string text=StringFormat("平仓失败,%s账号:%d,品种:%s_%s,编号:%d,错误代码:%d,错误内容:%s",IsRealAccount?"实盘":"模拟",AccountId,_Symbol,StringSubstr(EnumToString(_Period),7),Serial,retCode,transReturnCode(retCode));
               printf(text);
               string title=StringFormat("平仓失败,%s账号:%d,品种:%s_%s,编号:%d",IsRealAccount?"实盘":"模拟",AccountId,_Symbol,StringSubstr(EnumToString(_Period),7),Serial);
               SendMail(title, text);
               SendNotification(text);
               return;
              }
           }
         while(i<=20);
         //发送平仓邮件
         string text=StringFormat("平仓成功,%s账号:%d,品种:%s_%s,编号:%d,平仓价格:%f,方向:%s,盈亏比:%.2f,是否盈利:%s,平仓时间:%s",IsRealAccount?"实盘":"模拟",AccountId,_Symbol,StringSubstr(EnumToString(_Period),7),Serial,tradeInfo.outPrice,
                                  tradeInfo.direction==ORDER_TYPE_SELL ? "买平":"卖平",tradeInfo.profitRatio,tradeInfo.isWin?"是":"否",TimeToString(tradeInfo.outTime, TIME_DATE|TIME_SECONDS));
         printf(text);
         //发送平仓邮件
         string title=StringFormat("平仓成功,%s账号:%d,品种:%s_%s,编号:%d",IsRealAccount?"实盘":"模拟",AccountId,_Symbol,StringSubstr(EnumToString(_Period),7),Serial);
         SendMail(title, text);
         //发送平仓通知
         SendNotification(text);
        }
     }
//---
  }
//错误代码翻译
string transReturnCode(uint returnCode)
  {
   if(returnCode==TRADE_RETCODE_NO_MONEY)
     {
      return "资金(保证金)不足";
     }
   if(returnCode==TRADE_RETCODE_POSITION_CLOSED)
     {
      return "持仓已关闭";
     }
   if(returnCode==TRADE_RETCODE_MARKET_CLOSED)
     {
      return "收市";
     }
   if(returnCode==TRADE_RETCODE_REQUOTE)
     {
      return "重新报价";
     }
   if(returnCode==TRADE_RETCODE_CONNECTION)
     {
      return "与服务器无连接";
     }
   if(returnCode==TRADE_RETCODE_LIMIT_ORDERS)
     {
      return "待办订单数量达到限制";
     }
   if(returnCode==TRADE_RETCODE_REJECT)
     {
      return "拒绝请求";
     }
   if(returnCode==TRADE_RETCODE_INVALID)
     {
      return "无效请求";
     }
   if(returnCode==TRADE_RETCODE_INVALID_VOLUME)
     {
      return "无效成交量";
     }
   if(returnCode==TRADE_RETCODE_INVALID_PRICE)
     {
      return "无效价格";
     }
   if(returnCode==TRADE_RETCODE_INVALID_STOPS)
     {
      return "无效止损价";
     }
   if(returnCode==TRADE_RETCODE_TRADE_DISABLED)
     {
      return "关闭交易";
     }
   if(returnCode==TRADE_RETCODE_TOO_MANY_REQUESTS)
     {
      return "太频繁的请求";
     }
   return "未知错误";
  }
//+------------------------------------------------------------------+
//| Position select depending on netting or hedging                  |
//+------------------------------------------------------------------+
bool SelectPosition()
  {
   bool res=false;
//--- check position in Hedging mode
   if(ExtHedging)
     {
      uint total=PositionsTotal();
      for(uint i=0; i<total; i++)
        {
         string position_symbol=PositionGetSymbol(i);
         if(_Symbol==position_symbol && MA_MAGIC==PositionGetInteger(POSITION_MAGIC))
           {
            //填充持仓方向.开仓价位,止损价位等
            if(tradeInfo.direction==0)
              {
               tradeInfo.direction=PositionGetInteger(POSITION_TYPE)==POSITION_TYPE_BUY ? 1:2;
               tradeInfo.stopPrice=PositionGetDouble(POSITION_SL);
               tradeInfo.moveStopPrice=tradeInfo.stopPrice;
               tradeInfo.entryPrice=PositionGetDouble(POSITION_PRICE_OPEN);
               tradeInfo.lot=PositionGetDouble(POSITION_VOLUME);
               string comment=PositionGetString(POSITION_COMMENT);
               if(comment!="")
                 {
                  Serial=StringToInteger(comment);
                 }
              }
            res=true;
            break;
           }
        }
     }
//--- check position in Netting mode
   else
     {
      if(!PositionSelect(_Symbol))
         return(false);
      else
         if(PositionGetInteger(POSITION_MAGIC)==MA_MAGIC) //---check Magic number
           {
            //填充持仓方向.开仓价位,止损价位等
            if(tradeInfo.direction==0)
              {
               tradeInfo.direction=PositionGetInteger(POSITION_TYPE)==POSITION_TYPE_BUY ? 1:2;
               tradeInfo.stopPrice=PositionGetDouble(POSITION_SL);
               tradeInfo.moveStopPrice=tradeInfo.stopPrice;
               tradeInfo.entryPrice=PositionGetDouble(POSITION_PRICE_OPEN);
               tradeInfo.lot=PositionGetDouble(POSITION_VOLUME);
               string comment=PositionGetString(POSITION_COMMENT);
               if(comment!="")
                 {
                  Serial=StringToInteger(comment);
                 }
              }
            return true;
           }
      return(false);
     }
//--- result for Hedging mode
   return(res);
  }
//+------------------------------------------------------------------+
//| Expert initialization function                                   |
//+------------------------------------------------------------------+
int OnInit(void)
  {
//--- prepare trade class to control positions if hedging mode is active
   ExtHedging=((ENUM_ACCOUNT_MARGIN_MODE)AccountInfoInteger(ACCOUNT_MARGIN_MODE)==ACCOUNT_MARGIN_MODE_RETAIL_HEDGING);
   ExtTrade.SetExpertMagicNumber(MA_MAGIC);
   ExtTrade.SetMarginMode();
   ExtTrade.SetTypeFillingBySymbol(Symbol());
   ContractSize=SymbolInfoDouble(_Symbol,SYMBOL_TRADE_CONTRACT_SIZE);
   SymbolPoint=SymbolInfoDouble(_Symbol,SYMBOL_POINT);
   AccountId=AccountInfoInteger(ACCOUNT_LOGIN);
   IsRealAccount=AccountInfoInteger(ACCOUNT_TRADE_MODE)==ACCOUNT_TRADE_MODE_REAL;
//初始化数据
//交易周期指标加载
   ExtHandleMa35=iMA(_Symbol,_Period,35,0,MODE_EMA,PRICE_CLOSE);
   ExtHandleMa40=iMA(_Symbol,_Period,40,0,MODE_EMA,PRICE_CLOSE);
   ExtHandleMa45=iMA(_Symbol,_Period,45,0,MODE_EMA,PRICE_CLOSE);
   ExtHandleMa50=iMA(_Symbol,_Period,50,0,MODE_EMA,PRICE_CLOSE);
   ExtHandleMa60=iMA(_Symbol,_Period,60,0,MODE_EMA,PRICE_CLOSE);
   ExtHandleBands=iBands(_Symbol,_Period,20,0,2,PRICE_CLOSE);
   ExtHandleMa20=iMA(_Symbol,_Period,20,0,MODE_EMA,PRICE_CLOSE);
   ExtHandleMa220=iMA(_Symbol,_Period,220,0,MODE_EMA,PRICE_CLOSE);
//大周期指标加载
   ExtHandleMainMa35=iMA(_Symbol,MainTimeFrames,35,0,MODE_EMA,PRICE_CLOSE);
   ExtHandleMainMa40=iMA(_Symbol,MainTimeFrames,40,0,MODE_EMA,PRICE_CLOSE);
   ExtHandleMainMa45=iMA(_Symbol,MainTimeFrames,45,0,MODE_EMA,PRICE_CLOSE);
   ExtHandleMainMa50=iMA(_Symbol,MainTimeFrames,50,0,MODE_EMA,PRICE_CLOSE);
   ExtHandleMainMa60=iMA(_Symbol,MainTimeFrames,60,0,MODE_EMA,PRICE_CLOSE);
//加载全局序号
   GlobalSerialName=StringFormat("%s_%s_%d_Serial",_Symbol,StringSubstr(EnumToString(_Period),7),MA_MAGIC);
   if(GlobalVariableCheck(GlobalSerialName))
     {
      Serial=GlobalVariableGet(GlobalSerialName);
     }
//是否有仓位
   IsPostioned=SelectPosition();
//--- ok
   return(INIT_SUCCEEDED);
  }
//+------------------------------------------------------------------+
//| Expert tick function                                             |
//+------------------------------------------------------------------+
void OnTick(void)
  {
//---
   IsPostioned=SelectPosition();
   if(IsPostioned)
      CheckForClose();
   else
      CheckForOpen();
//---
  }
//+------------------------------------------------------------------+
//| Expert deinitialization function                                 |
//+------------------------------------------------------------------+
void OnDeinit(const int reason)
  {
  }
//+------------------------------------------------------------------+

//+------------------------------------------------------------------+
//| Trade function                                                   |
//+------------------------------------------------------------------+
void OnTrade()
  {
//---

  }
//+------------------------------------------------------------------+
//| TradeTransaction function                                        |
//+------------------------------------------------------------------+
void OnTradeTransaction(const MqlTradeTransaction& trans,
                        const MqlTradeRequest& request,
                        const MqlTradeResult& result)
  {
//---

  }
//+------------------------------------------------------------------+
